
# Moving Fret on a String
# Author: James Leonard

@strK param 0.6

@s stiffString 120 1. strK 0.2 0.001 0.00003 0. 0.

@m mass 100000 0. 0.
@gm mass 1000000 -1. 0.

# First input is bow height
@in1 posInput 0.

# Finger height & 'Fretboard' height
@in3 posInput 0.
@in4 posInput 0.

# Audio-rate parameter for fret position
@slide audioParam @in2

@fK param 0.7
@fZ param 0.05

# The mass that will press down on the string
@spm springDamper @in3 @m 10000 10000
@spm2 springDamper @in4 @gm 10000 10000

# Finger on string contacts
@fs1 contact [@s slide] @m fK 0. 
@fs2 contact [@s slide-0.01] @m fK 0.

# String on fretboard contacts
@sb1 contact @gm [@s slide] fK 0 
@sb2 contact @gm [@s slide-0.01] fK 0.

# Damping contacts behind finger
@d1 contact [@s slide-0.04] @m 0.2 fZ 
@d2 contact [@s slide-0.03] @m 0.2 fZ
@d3 contact [@s slide-0.1] @m 0.02 fZ
@d4 contact [@s slide-0.2] @m 0.02 fZ

@bowPos audioParam @in5
@bowPres audioParam @in6

@type param 1

@b nlBow [@s bowPos] @in1 0.1*bowPres [0.002+0.004*bowPres] type
@b2 nlBow [@s bowPos-0.03] @in1 0.1*bowPres [0.002+0.004*bowPres] type
@b3 nlBow [@s bowPos-0.06] @in1 0.1*bowPres [0.002+0.004*bowPres] type

@out1 posOutput [@s 0.96]
@out2 posOutput [@s 0.92]


